PREFIX dbc: <http://dbpedia.org/resource/Category:>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT 
  (REPLACE(REPLACE(STRAFTER(STR(?philosopher), "http://dbpedia.org/resource/"), "_", " "), ", .*", "") AS ?name)
  (STR(?birthDate) AS ?birthDateStr)
  (GROUP_CONCAT(DISTINCT REPLACE(STRAFTER(STR(?birthPlace), "http://dbpedia.org/resource/"), "_", " "); SEPARATOR="; ") AS ?birthPlaces)
  (GROUP_CONCAT(DISTINCT REPLACE(STRAFTER(STR(?nationality), "http://dbpedia.org/resource/"), "_", " "); SEPARATOR="; ") AS ?nationalities)
  (GROUP_CONCAT(DISTINCT REPLACE(REPLACE(STRAFTER(STR(?influence), "http://dbpedia.org/resource/"), "_", " "), ", .*", ""); SEPARATOR="; ") AS ?influencedByList)
WHERE {
  ?philosopher dct:subject dbc:French_philosophers .

  # Filter only the ones with a "proper" birthdate
  ?philosopher dbo:birthDate ?birthDate .
  FILTER(datatype(?birthDate) = xsd:date || datatype(?birthDate) = xsd:dateTime)

  OPTIONAL { ?philosopher dbo:birthPlace ?birthPlace }
  OPTIONAL { ?philosopher dbo:nationality ?nationality }
  OPTIONAL { ?philosopher dbo:influencedBy ?influence }
}
GROUP BY ?philosopher ?birthDate
ORDER BY ASC(?birthDate)
